; VAX - everything but X11 netbsd
; stop at each option for debuging and alternate manual selection 

set env INSTALL=step
echo "install type is %INSTALL%"

do env-shared.ini
do install-shared.ini
do global-shared.ini


:network
attach xq0 nat:

call boot

; boot
boot cpu
; exit does not work at end of install
; so keeping here to see there it does work
; prevents continuing tho
exit


:boot
; set default boot to disk dua0
; must have NVRAM file attach to persist
expect ">>>" send "set boot dua0"; continue
; boot install cd
expect ">>>"  send "boot dua1"; call initialize; continue
return


:initialize 
; installer is menu driven
; accept vt220 terminal
; expect "Terminal type" send "\r"; continue
; use english
; expect "enter key." send "\r"; continue 
; install to disk
; expect "Install System" send "\r"; continue
; yes, really
expect "quit this procedure" send "b"; continue
; install on disk ra0
expect "Available disks" send "a"; continue
; use default partitioning
expect "Use default partition sizes" send "b"; continue
; Partition sizes ok 
expect "Partition sizes ok" send "x"; continue
; withoutX
expect "This is your last chance" call withoutX; send "b"; continue
return


:withoutX
; b -  
; does not include X11
; matching the last string output so that they full menu is visible
; use minimal "c" when testing to reduce download 
; expect "x: Abandon installation" send "c"; continue
expect "x: Abandon installation" send "b"; continue
; use http
expect "j: Abandon installation" call networking; send "b"; continue
return


:networking
; use default network interface
expect "Available interfaces" send "a"; continue
; use default media type
; expect "Network media type (empty to autoconfigure):" send "\r"; continue
; auto configure network
expect "Perform autoconfiguration?" send "a"; continue
; hostname
expect "Your host name:" send "%HOSTNAME%";  continue
; no domain
; expect "Your DNS domain:" send "\r"; continue
; accept network configuration
expect "Primary Interface" call distribution; send "a"; continue
; expect "Primary Interface" send "a"; continue
return


:distribution
; get distribution
; TODO
; something wrong with the display here so I cannot see the menu.
; but hitting enter seems to work
; probably need to match the last output.  See withoutX:
expect "Get Distribution" send "x"; continue
; persist network config
expect "want it installed in /etc?" send "a"; continue
; additional configuration
expect "essential things first." continue
; root password
; expect "Changing local password for root" send "%ROOT_PASSWD%"; continue
; expect "Retype new password:" send "%ROOT_PASSWD%"; continue
; entropy
expect "Manually input random characters" send "a"; continue
expect "may not be secure." call configure; send "%ENTROPY%"; continue
return


:configure
; 600 commands
; configure options from main menu
; enable/disable until add a user 
; enable sshd
expect "Enable sshd" send "g"; send "\r"; continue
; enable ntpd
expect "Enable ntpd" send "h"; send "\r"; continue
; run ntpd on boot
expect "Run ntpdate at boot" send "i"; send "\r"; continue
; disable cgd
expect "Enable cgd" send "l"; send "\r"; continue
; disable raidframe
expect "Enable raidframe" send "n"; send "\r"; continue
; add a user
; 610
expect "Add a user" send "o"; continue
expect "8 character username to add" send "%USER%"; continue
expect "Do you wish to add this user to group wheel?" send "a"; continue
expect "User shell" call finish; send "a"; continue
; expect "Changing local password for %USER%" send "%USER_PASSWD%"; continue
; expect "Retype new password:" call finish; send "%USER_PASSWD%"; continue
return


:finish
; final step of configure
expect "Finished configuring" send "x"; continue
; 900
expect "The installation of NetBSD-%NETBSD_VERSION% is now complete" continue
; done
; reboot makes the system exit which is what I want
; expect "Exit Install System" send "d"; continue
return
