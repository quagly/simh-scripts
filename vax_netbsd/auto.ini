; VAX - netbsd without X11
; fully automated except doesn't exit at the end from simh
;
; TODO
; expect rules remaining after full successful run on 2024-03-02
; I tried removing boot dua rules but then it didn't boot
; are they somehow execution but not being removed?  
; EXPECT "Available interfaces" send "a"; send "\r"; continue
; EXPECT "Network media type:" send "\r"; continue
; EXPECT "Perform autoconfiguration?" send "a"; send "\r"; continue
; EXPECT "Your host name:" send "%HOSTNAME%"; send "\r" ; continue
; EXPECT "Your DNS domain:" send "\r"; continue
; EXPECT "Primary Interface" call distribution; send "a"; send "\r"; continue
; EXPECT ">>>" send "set boot dua0"; send "\r"; continue
; EXPECT ">>>" send "boot dua1"; call initialize; send "\r"; continue

set env INSTALL=auto
echo "install type is %INSTALL%"

do env-shared.ini
do install-shared.ini
do global-shared.ini


; networking should go in global.ini once tun/tap is working
:NETWORK
attach xq0 nat:
call boot


; boot
boot cpu
exit 0


:boot
; set default boot to disk dua0
; must have NVRAM file attach to persist
expect ">>>" send "set boot dua0"; send "\r"; continue
; boot install cd
expect ">>>"  send "boot dua1"; call initialize; send "\r"; continue
return


:initialize 
; accept vt220 terminal
expect "Terminal type" send "\r"; continue
; use english
expect "enter key." send "\r"; continue 
; install to disk
expect "Install System" send "\r"; continue
; yes, really
expect "quit this procedure" send "b"; send "\r"; continue
; install on disk ra0
expect "Available disks" send "a"; send "\r"; continue
; use default partitioning
expect "Use default partition sizes" send "b"; send "\r"; continue
; Partition sizes ok 
expect "Partition sizes ok" send "x"; send "\r"; continue
; withoutX
expect "This is your last chance" call withoutX; send "b"; send "\r"; continue
return


:withoutX
; does not include X11
; trying out expecting the last output so that the who menu is viewable when sleeping, works well
expect "x: Abandon installation" call networking; send "b"; send "\r"; continue
; use http
expect "j: Abandon installation" call networking; send "b"; send "\r"; continue
return


:networking
; use default network interface
expect "Available interfaces" send "a"; send "\r"; continue
; use default media type
expect "Network media type (empty to autoconfigure):" send "\r"; continue
; auto configure network
expect "Perform autoconfiguration?" send "a"; send "\r"; continue
; hostname
expect "Your host name:" send "%HOSTNAME%"; send "\r" ; continue
; no domain
expect "Your DNS domain:" send "\r"; continue
; accept network configuration
expect "Primary Interface" call distribution; send "a"; send "\r"; continue
return


:distribution
; get distribution
; first two commands don't get called from manual installation so maybe move them to networking if auto-installation works
expect "Get Distribution" send "x"; send "\r"; continue
; persist network config
expect "want it installed in /etc?" send "a"; send "\r"; continue
; additional configuration
expect "essential things first." send "\r"; continue
; root password
; expect "Changing local password for root" send "%ROOT_PASSWD%"; send "\r"; continue
; expect "Retype new password:" send "%ROOT_PASSWD%"; send "\r"; continue
expect "Changing local password for root" send "\r"; continue
; expect "Retype new password:" send "\r"; continue
; entropy
expect "Manually input random characters" send "a"; send "\r"; continue
expect "may not be secure." call configure; send "%ENTROPY%"; send "\r"; continue
return


:configure
; configure options from main menu
; enable sshd
expect "Enable sshd" send "g"; send "\r"; continue
; enable ntpd
expect "Enable ntpd" send "h"; send "\r"; continue
; run ntpd on boot
expect "Run ntpdate at boot" send "i"; send "\r"; continue
; disable cgd
expect "Enable cgd" send "l"; send "\r"; continue
; disable raidframe
expect "Enable raidframe" send "n"; send "\r"; continue
; add a user
expect "Add a user" send "o"; send "\r"; continue
expect "8 character username to add" send "%USER%"; send "\r"; continue
expect "Do you wish to add this user to group wheel?" send "a"; send "\r"; continue
expect "User shell" send "a"; send "\r"; continue
; expect "Changing local password for %USER%" send "%USER_PASSWD%"; send "\r"; continue
; expect "Retype new password:" call finish; send "%USER_PASSWD%"; send "\r"; continue
expect "Changing local password for %USER%" call finish; send "\r"; continue
; expect "Retype new password:" call finish; send "\r"; continue
return


:finish
; final step of configure
expect "Finished configuring" send "x"; send "\r"; continue
; 900
expect "The installation of NetBSD-%NETBSD_VERSION% is now complete" send "\r";  continue
; reboot will cause exit 
expect "Exit Install System" send "d"; send "\r"; continue
return
